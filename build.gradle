plugins {
    id 'eclipse'
    id 'idea'

    id 'org.spongepowered.plugin' version '0.8.1'
    id 'org.jetbrains.kotlin.jvm' version '1.1.3'
    id 'org.jetbrains.kotlin.kapt' version '1.1.3'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id 'net.minecrell.licenser' version '0.3'
}

group = pluginGroup
version = pluginVersion

repositories {
    mavenCentral()
    maven {url "https://repo.spongepowered.org/maven/"}
    maven {url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"}
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

configurations {
    compile.extendsFrom shadow
}

compileJava {
    options.annotationProcessorPath = configurations.kapt
}

dependencies {
    compile "org.spongepowered:spongeapi:$spongeVersion"
    kapt "org.spongepowered:spongeapi:$spongeVersion"
    shadow 'org.jetbrains.kotlin:kotlin-stdlib-jre8'
    shadow 'org.jetbrains.kotlin:kotlin-runtime'
    shadow 'org.jetbrains.kotlin:kotlin-reflect'
}

sponge {
    plugin {
        id = pluginId
        meta {
            authors = ['KingGoesGaming', 'pie_flavor']
        }
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    relocate "kotlin", "spotlin"
    classifier = "$kotlinVersion" // Remove '-all' suffix
}

build.dependsOn shadowJar

license {
    header = project.file('HEADER')
    include '**/*.kt'
}